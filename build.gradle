buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5' // Add this line
        classpath 'com.mobidevelop.robovm:robovm-gradle-plugin:2.3.7'
        classpath 'org.multi-os-engine:moe-gradle:1.4.3'
    }
}


allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.7
        targetCompatibility = 1.7
    }

    version = '1.0'
    ext {
        appName = "gdx-var3d-1.9.9"
        gdxVersion = '1.9.9'
        roboVMVersion = '2.3.7'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}



project(":desktop") {
    apply plugin: "java"
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
    }
}

project(":ios") {
    apply plugin: "java"
    apply plugin: "robovm"
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile project(":core")
        compile "com.mobidevelop.robovm:robovm-rt:$roboVMVersion"
        compile "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion"
        compile "com.badlogicgames.gdx:gdx-backend-robovm:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    }
}

project(":ios-moe") {
    apply plugin: "moe"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-moe:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"

    }
}

project(":core") {
    apply plugin: "java"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile fileTree(dir: 'libs', include: '*.jar')
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

subprojects {

    task packJar(type: Jar) {

        doFirst {
            if(project.name == "android"){
                from fileTree(dir: "build/intermediates/classes/core/release/", includes: ["**/VAndroidLauncher.class"], exclude: "**/demo")
            }else{
                from sourceSets.core.output.classesDir
            }
        }
    }



    def preCompile = [":core:coreClasses", ":desktop:coreClasses", ":ios:coreClasses",":ios-moe:coreClasses", ":android:compileCoreReleaseJavaWithJavac"];

    packJar.dependsOn preCompile

    task packDoc(dependsOn: ":core:javadoc", type: Jar) {
        classifier = "javadoc"
        if (project.name == "core"){
            from project(":core").tasks["javadoc"].destinationDir
        }

    }

    task packSources(type: Jar) {
        classifier = "sources"
        doFirst {
            if(project.name == "android"){
                from files("src/core/java")
            }else {
                from sourceSets.core.allJava
            }

        }
    }


    task packAll(dependsOn: ['packJar', 'packDoc', 'packSources'])

    artifacts {
        archives packJar
        archives packDoc
        archives packSources
    }
}
